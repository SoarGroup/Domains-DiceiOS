#Maybe should subtract my unknowns - doesn't want to depend on them.

sp {compute-bid-probability-with-assumed-dice*propose*compute-bid-probability
   (state <s> ^name compute-bid-probability-with-assumed-dice
              ^dice-counts.dice-count.face unknown
              ^superstate.operator.holds-probability
              ^superstate.last-bid <so>)
   (<so> ^face <face>
         ^multiplier <multipler>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name compute-bid-probability
         ^face <face>
         ^multiplier <multipler>)
}

## 1 bid
sp {apply*compute-bid-probability*1*bidxx
   (state <s> ^name compute-bid-probability-with-assumed-dice
              ^operator <op>
              ^top-state.io.output-link <out>
              ^dice-counts.dice-count <dc1> <dc2>)
   (<op> ^name compute-bid-probability
         ^face 1
         ^multiplier <bid-multi>)
   (<dc1> ^face 1
          ^count <multi-1>)
   (<dc2> ^face unknown
          ^count <multi-unknown>)
-->
   (write (crlf) |  Bid: | <bid-multi> | 1's. Known: | <multi-1> |. Unknown: | <multi-unknown> |. Estimated total: |
   (+ (/ <multi-unknown> 6) <multi-1>) | Bid Difference: |
   (- (+ (/ <multi-unknown> 6) <multi-1>) <bid-multi>))
   (<s> ^total-known (- <multi-1> <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <multi-1>) <bid-multi>))
   (<out> ^qna-query <qq>)
   (<s> ^evaluation <qq>)
   (<qq> ^query compute-probability
         ^source dice
         ^results all
         ^parameters <par>)
   (<par> ^count (- <bid-multi> <multi-1>)
          ^number-of-dice <multi-unknown>
          ^number-of-faces 6
          ^predicate ge)
}

#####

sp {apply*compute-bid-probability*special-rules*bidxx
   (state <s> ^name compute-bid-probability-with-assumed-dice
              ^operator <op>
              ^top-state.io <io>
              ^dice-counts.dice-count <dc1> <dc2>)
   (<op> ^name compute-bid-probability
         ^face { <> 1 <face> }
         ^multiplier <bid-multi>)
   (<io> ^input-link.state.special true
         ^output-link <out>)
   (<dc1> ^face <face>
          ^count <multi-1>)
   (<dc2> ^face unknown
          ^count <multi-unknown>)
-->
   (write (crlf) |  Bid: | <bid-multi> | | <face> |'s. Estimated total: | (+ (/ <multi-unknown> 6) <multi-1>) | Bid Difference: | (- (+ (/ <multi-unknown> 6) <multi-1>) <bid-multi>) | special rules|)
   (<s> ^total-known (- <multi-1> <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <multi-1>) <bid-multi>))
   (<out> ^qna-query <qq>)
   (<s> ^evaluation <qq>)
   (<qq> ^query compute-probability
         ^source dice
         ^results all
         ^parameters <par>)
   (<par> ^count (- <bid-multi> <multi-1>)
          ^number-of-dice <multi-unknown>
          ^number-of-faces 6
          ^predicate ge)}

sp {apply*compute-bid-probability*not-special-rules*bidxx
   (state <s> ^name compute-bid-probability-with-assumed-dice
              ^operator <op>
              ^top-state <ts>
              ^dice-counts.dice-count <dc1> <dc2> <dc3>)
   (<op> ^name compute-bid-probability
         ^face { <> 1 <face> }
         ^multiplier <bid-multi>)
   (<ts> ^io.input-link.state.special false
         ^io.output-link <out>)
   (<dc1> ^face <face>
          ^count <multi-bid>)
   (<dc2> ^face 1
          ^count <multi-1>)
   (<dc3> ^face unknown
          ^count <multi-unknown>)
-->
   (write (crlf) |  Bid: | <bid-multi> | | <face> |'s. Estimated total: | (+ (/ <multi-unknown> 3) <multi-1> <multi-bid>) | Bid Difference: | (- (+ (/ <multi-unknown> 3) <multi-1> <multi-bid>) <bid-multi>) | normal rules|)
   (<s> ^total-known (- (+ <multi-bid> <multi-1>) <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <multi-bid> <multi-1>) <bid-multi>))
   (<out> ^qna-query <qq>)
   (<s> ^evaluation <qq>)
   (<qq> ^query compute-probability
         ^source dice
         ^results all
         ^parameters <par>)
   (<par> ^count (- <bid-multi> (+ <multi-bid> <multi-1>))
          ^number-of-dice <multi-unknown>
          ^number-of-faces 3
          ^predicate ge)
}

sp {compute-bid-probability-assumed-dice*apply*compute-challenge-probability*use-resultxx
   (state <s> ^operator <op>
              ^evaluation <qq>
              ^superstate.operator <so>)
   (<op> ^name compute-bid-probability)
   (<qq> ^result.features.probability <prob>)
-->
   (write (crlf) |>>>Probability: | <prob>)
   (<so> ^probability <prob>)
}

