
sp {selection*prefer*best*bid*pass*certain*no-push
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e>)
   (<e> ^superoperator <so>
        ^symbolic-value certain)
   (<so> ^name << bid pass >>
        -^push-face)
-->
   (<ss> ^operator <so> = 0.95)}

sp {selection*compare*bid-better-challenge*symbolic
  (state <s> ^name selection
             ^superstate <ss>
             ^evaluation <e1> { <> <e1> <e2> })
  (<ss> ^desired <d>)
  (<e1> ^superoperator <x>
        ^symbolic-value << safe certain >>
        ^desired <d>)
  (<e2> ^superoperator { <> <x> <y> }
       ^symbolic-value <> certain
        ^desired <d>)
  (<x> ^name << bid pass >>)
  (<y> ^name challenge)
-->
  (<ss> ^operator <x> > <y>)}
  
sp {evaluate-operator*propose*compute-bid-probability
   (state <s> ^name evaluate-operator
              ^top-state.probabilities true
              ^top-state.io.input-link.state.last-bid <> nil 
              ^superstate.operator.superoperator <so>)
   (<so> ^name bid
        -^push-face
         ^face <face>
         ^multiplier <multipler>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name compute-bid-probability
         ^face <face>
         ^multiplier <multipler>)
}
 
 
sp {evaluate-operator*propose*compute-bid-push-probability
   (state <s> ^name evaluate-operator
              ^top-state.io.input-link.state.last-bid <> nil
              ^top-state.probabilities true
              ^superstate.operator.superoperator <so>)
   (<so> ^name bid
         ^push-face-count
         ^face <face>
         ^multiplier <multipler>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name compute-bid-push-probability
         ^face <face>
         ^multiplier <multipler>)
}

excise selection*apply*create-preferences*save*probability



sp {selection*select*failure-evaluation-becomes-reject-preference
  :default
  (state <s> ^name selection
             ^superstate <ss>
             ^evaluation <e1>)
  (<ss> ^desired <e>)
  (<e1> ^symbolic-value << lose failure >>
        ^desired <e>
        ^superoperator <o1>)
-->
  (<ss> ^operator <o1> = 0.05)
}

sp {selection*select*lose-evaluation-becomes-worst-preference # invalid pass challenge
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e1>)
   (<ss> ^desired <eb>)
   (<e1> ^symbolic-value lose
         ^desired <eb>
         ^superoperator <o1>)
-->
   (<ss> ^operator <o1> = 0.05)}


excise selection*compare*safe*equal*certain

