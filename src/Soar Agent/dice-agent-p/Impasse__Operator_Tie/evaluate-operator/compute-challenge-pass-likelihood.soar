## Challenge pass 

sp {evaluate-operator*propose*compute-challenge-pass-likelihood
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.io.input-link.history <history>
              ^operator <op> +)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<history> ^action pass  
              ^player.name <name>) #Make sure we challenge the last pass 
   (<so> ^name challenge
         ^last-bid-type pass
         ^target <t>)
   (<t> ^name <name>)
-->
   (<op> ^name compute-challenge-pass-likelihood
         ^target <t>)
}

sp {evaluate-operator*propose*compute-challenge-push-pass-likelihood
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.io.input-link.history <history>
              ^operator <op> +)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<history> ^action push
			  ^next.action pass
              ^player.name <name>) #Make sure we challenge the last pass 
   (<so> ^name challenge
         ^last-bid-type pass
         ^target <t>)
   (<t> ^name <name>)
-->
   (<op> ^name compute-challenge-pass-likelihood
         ^target <t>)
}

## Challenge a pass before a pass
sp {evaluate-operator*propose*compute-challenge-pass-pass-likelihood
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.io.input-link.history.next <history> # next gives previous bid
              ^operator <op> +)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<history> ^action pass  
              ^player.name <name>) #Make sure we challenge the last pass 
   (<so> ^name challenge
         ^last-bid-type pass-pass
         ^target <t>)
   (<t> ^name <name>)
-->
    (<op> ^name compute-challenge-pass-likelihood
         ^target <t>)
}

sp {evaluate-operator*propose*compute-challenge-push-pass-pass-likelihood
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.io.input-link.history <history>
              ^operator <op> +)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<history> ^action push
              ^next.action pass
		      ^next.next.action pass
              ^next.next.player.name <name>) #Make sure we challenge the last pass
   (<so> ^name challenge
         ^last-bid-type pass-pass
         ^target <t>)
   (<t> ^name <name>)
-->
    (<op> ^name compute-challenge-pass-likelihood
         ^target <t>)
}

sp {evaluate-operator*propose*compute-challenge-pass-push-pass-likelihood
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.io.input-link.history <history>
              ^operator <op> +)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<history> ^action pass
              ^next.action push
		      ^next.next.action pass
              ^next.next.player.name <name>) #Make sure we challenge the last pass
   (<so> ^name challenge
         ^last-bid-type pass-pass
         ^target <t>)
   (<t> ^name <name>)
-->
    (<op> ^name compute-challenge-pass-likelihood
         ^target <t>)
}

sp {evaluate-operator*propose*compute-challenge-push-pass-push-pass-likelihood
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.io.input-link.history <history>
              ^operator <op> +)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<history> ^action push
              ^next.action pass
		      ^next.next.action push
		      ^next.next.next.action pass
              ^next.next.next.player.name <name>) #Make sure we challenge the last pass
   (<so> ^name challenge
         ^last-bid-type pass-pass
         ^target <t>)
   (<t> ^name <name>)
-->
    (<op> ^name compute-challenge-pass-likelihood
         ^target <t>)
}

sp {apply*compute-challenge-pass-likelihood*impossible*pass*prob-only
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target.pushed <push1>)
   (<push1> ^die.face <n>
            ^die.face <> <n>)
-->
   (write (crlf) |  Different faces exposed: Pass challenge success likelihood is 1.0|)
   (<e> ^probability 1.0)
}

#############
sp {apply*compute-challenge-pass-likelihood*unlikely*pass*all-under*three
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target <target>)
   (<target> ^cup.count 3
             ^pushed.count 0)
-->
   (write (crlf) |  Three unknown. Pass challenge success likelihood is .95|)
   (<e> ^probability .95)}

sp {apply*compute-challenge-pass-likelihood*unlikely*pass*all-under*more-three
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target <target>)
   (<target> ^cup.count > 3
             ^pushed.count 0)
-->
   (write (crlf) |  More than three unknown: Pass challenge success likelihood is 1.0|)
   (<e> ^probability 1.0)}

sp {apply*compute-challenge-pass-likelihood*reasonable*pass*all-under*2
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target <target>)
   (<target> ^cup.count 2
             ^pushed.count 0)
-->
   (write (crlf) |  Two unknown: Pass challenge success likelihood is .50|)
   (<e> ^probability .50)}

sp {apply*compute-challenge-pass-likelihood*reasonable*pass*1-under
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target <target>)
   (<target> ^cup <cup>)
   (<cup> ^count 1)
  -{(<target> ^pushed <push1>)
    (<push1> ^die.face <n>
             ^die.face <> <n>)}
-->
   (write (crlf) |  One under cup: Pass challenge success likelihood is .50|)
   (<e> ^probability .50)}

sp {apply*compute-challenge-pass-likelihood*unlikely*pass*not-all-under*two
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target <target>)
   (<target> ^cup.count 2
             ^pushed.count > 0)
 -{(<target> ^pushed <push1>)  # Add 5/15/2014 so it doesn't override certain challenge.
   (<push1> ^die.face <n>
            ^die.face <> <n>)}
-->
   (write (crlf) |  Two under cup and some pushed: Pass challenge success likelihood is .95|)
   (<e> ^probability .95)
}

sp {apply*compute-challenge-pass-likelihood*unlikely*pass*not-all-under*more-two
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <e>)
   (<op> ^name compute-challenge-pass-likelihood
         ^target <target>)
   (<target> ^cup.count > 2
             ^pushed.count > 0)
 -{(<target> ^pushed <push1>)  # Add 5/15/2014 so it doesn't override certain challenge.
   (<push1> ^die.face <n>
            ^die.face <> <n>)}
-->
   (write (crlf) |  More than two under cup and some pushed: Pass challenge success likelihood is 1.0|)
   (<e> ^probability 1.0)
}

