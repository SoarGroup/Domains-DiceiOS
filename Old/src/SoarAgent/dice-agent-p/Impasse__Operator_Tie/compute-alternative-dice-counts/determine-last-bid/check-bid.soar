sp {determine-last-bid*propose*check-bid
   (state <s> ^name determine-last-bid
              ^current-bid)
-->
   (<s> ^operator <op> +)
   (<op> ^name check-bid)
}

sp {apply*check-bid*skip*pass
   (state <s> ^operator.name check-bid
              ^current-bid <cb>)
   (<cb> ^action pass
         ^next <next>)
-->
   (<s> ^current-bid <cb> -
                     <next>)
}

## If looking for second prior, ignore pushes
sp {apply*check-bid*skip*push*second-bid
   (state <s> ^operator.name check-bid
              ^found-prior-bid true
              ^current-bid <cb>)
   (<cb> ^action push
         ^next <next>)
-->
   (<s> ^current-bid <cb> -
                     <next>)
}

sp {apply*check-bid*push*is-failure
   (state <s> ^operator.name check-bid
              ^superstate <ss>
              ^found-prior-bid false
              ^current-bid.action push)
-->
   (write (crlf) |Last bid was a push - ignore - failure.|)
   (<ss> ^last-bid failure)
}

sp {apply*check-bid*first-bid*not-me
   (state <s> ^operator.name check-bid
              ^superstate <ss>
              ^found-prior-bid false
              ^current-bid <cb>)
   (<cb> ^action bid
         ^multiplier <m>
         ^face <f>
        -^player.me true)
-->
   (write (crlf) |Last bid was a | <m> | | <f> |'s|)
   (<ss> ^last-bid <cb>)
}

## If find me that means no prior bid worth considering - passes
sp {apply*check-bid*first-bid*me
   (state <s> ^operator.name check-bid
              ^superstate <ss>
              ^found-prior-bid false
              ^current-bid <cb>)
   (<cb> ^action bid
         ^player.me true)
-->
   (write (crlf) |Last bid was by me - failure.|)
   (<ss> ^last-bid failure)
}

sp {apply*check-bid*first-bid-fail
   (state <s> ^operator.name check-bid
              ^superstate <ss>
              ^found-prior-bid false
              ^current-bid nil)
-->
   (write (crlf) |Did not find previous bid.|)
   (<ss> ^last-bid failure)
}

